apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.pedalfaster.launcher"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // set dummy signing values if not defined in ~/.gradle/gradle.properties
    if (!project.hasProperty('pedalfasterKeystore')) {
        println "Using dummy signing values"
        project.ext.setProperty('pedalfasterKeystore', 'dummy')
        project.ext.setProperty('pedalfasterKeystorePassword', 'dummy')
        project.ext.setProperty('pedalfasterKeyAlias', 'dummy')
        project.ext.setProperty('pedalfasterKeyPassword', 'dummy')
    }

    // defined values in ~/.gradle/gradle.properties
    signingConfigs {
        prodConfig {
            storeFile file(pedalfasterKeystore)
            storePassword pedalfasterKeystorePassword
            keyAlias pedalfasterKeyAlias
            keyPassword pedalfasterKeyPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.prodConfig
        }
        release {
            // add build timestamps and other items here
            signingConfig signingConfigs.prodConfig
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }

}

apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions' // ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    javacOptions {
        // Increase the max count of errors from annotation processors. (Default is 100)
        option("-Xmaxerrs", 500)
    }
}

ext {
    supportLibraryVersion = '28.0.0'
    playServicesVersion = '10.2.1'
    pocketKnifeVersion = '3.2.1'
    pocketbusVersion = '1.1.0'
    daggerVersion = '2.10'
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:preference-v14:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //noinspection GradleCompatible
    implementation 'com.android.support:multidex:1.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.evernote:android-job:1.1.8'

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.vikingsen:pocketbus:$pocketbusVersion"
    kapt "com.vikingsen:pocketbus-compiler:$pocketbusVersion"

    implementation "com.vikingsen:pocketknife-core:$pocketknifeVersion"
    kapt "com.vikingsen:pocketknife-compiler:$pocketknifeVersion"

    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation 'com.afollestad.material-dialogs:commons:0.9.4.2'

    implementation 'com.devbrackets.android:recyclerext:2.1.1'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    implementation 'com.andrognito.pinlockview:pinlockview:2.0.1'

    // Test
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.3-alpha') {
        exclude module: 'jsr305'
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:runner:0.6-alpha') {
        exclude module: 'jsr305'
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:0.6-alpha') {
        exclude module: 'jsr305'
        exclude module: 'support-annotations'
    }
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.threeten:threetenbp:1.3.3:'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    testImplementation 'org.xerial:sqlite-jdbc:3.16.1'
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
}

repositories {
    mavenCentral()
}
